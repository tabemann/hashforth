\ Copyright (c) 2019, Travis Bemann
\ All rights reserved.
\ 
\ Redistribution and use in source and binary forms, with or without
\ modification, are permitted provided that the following conditions are met:
\ 
\ 1. Redistributions of source code must retain the above copyright notice,
\    this list of conditions and the following disclaimer.
\ 
\ 2. Redistributions in binary form must reproduce the above copyright notice,
\    this list of conditions and the following disclaimer in the documentation
\    and/or other materials provided with the distribution.
\ 
\ 3. Neither the name of the copyright holder nor the names of its
\    contributors may be used to endorse or promote products derived from
\    this software without specific prior written permission.
\ 
\ THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
\ AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
\ IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
\ ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
\ LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
\ CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
\ SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
\ INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
\ CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
\ ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
\ POSSIBILITY OF SUCH DAMAGE.

BASE @ GET-CURRENT GET-ORDER

DECIMAL
FORTH-WORDLIST 1 SET-ORDER
FORTH-WORDLIST SET-CURRENT

WORDLIST CONSTANT BUFFER-WORDLIST
FORTH-WORDLIST BUFFER-WORDLIST 2 SET-ORDER
BUFFER-WORDLIST SET-CURRENT

BEGIN-STRUCTURE BUFFER-SIZE
  FIELD: BUFFER-DATA
  FIELD: BUFFER-DATA-COUNT
  FIELD: BUFFER-DATA-SIZE
END-STRUCTURE

: NEW-BUFFER ( init-bytes -- buffer )
  BUFFER-SIZE ALLOCATE!
  OVER ALLOCATE! OVER BUFFER-DATA !
  TUCK BUFFER-DATA-SIZE !
  0 OVER BUFFER-DATA-COUNT ! ;

: DESTROY-BUFFER ( buffer -- ) DUP BUFFER-DATA @ FREE! FREE! ;

: GET-BUFFER-LENGTH ( buffer -- bytes ) BUFFER-DATA-COUNT @ ;

: EXPAND-BUFFER ( bytes buffer -- )
  DUP BUFFER-DATA-COUNT @ ROT + OVER BUFFER-DATA-SIZE @ 2 * MAX
  OVER BUFFER-DATA @ OVER RESIZE! 2 PICK BUFFER-DATA !
  SWAP BUFFER-DATA-SIZE ! ;

: APPEND-BUFFER ( c-addr bytes buffer -- )
  DUP BUFFER-DATA-COUNT @ 2 PICK + OVER BUFFER-DATA-SIZE @ > IF
    2DUP EXPAND-BUFFER
  THEN
  DUP BUFFER-DATA @ OVER BUFFER-DATA-COUNT @ + 3 ROLL SWAP 3 PICK MOVE
  BUFFER-DATA-COUNT +! ;

: APPEND-BYTE-BUFFER ( c buffer -- )
  DUP BUFFER-DATA-COUNT @ OVER BUFFER-DATA-SIZE @ >= IF
    1 OVER EXPAND-BUFFER
  THEN
  DUP BUFFER-DATA @ OVER BUFFER-DATA-COUNT @ + ROT SWAP C!
  1 SWAP BUFFER-DATA-COUNT +! ;

: REMOVE-BUFFER ( bytes offset buffer -- )
  DUP BUFFER-DATA @ 3 PICK + 2 PICK + OVER BUFFER-DATA @ 3 PICK +
  2 PICK BUFFER-DATA-COUNT @ 5 PICK - MOVE
  ROT OVER BUFFER-DATA-COUNT @ SWAP - SWAP BUFFER-DATA-COUNT ! DROP ;

: REMOVE-START-BUFFER ( bytes buffer -- )
  DUP BUFFER-DATA @ 2 PICK + OVER BUFFER-DATA @ 2 PICK BUFFER-DATA-COUNT @
  4 PICK - MOVE DUP BUFFER-DATA-COUNT @ ROT - SWAP BUFFER-DATA-COUNT ! ;

: REMOVE-END-BUFFER ( bytes buffer -- )
  DUP BUFFER-DATA-COUNT @ ROT - SWAP BUFFER-DATA-COUNT ! ;

: PREPEND-BUFFER ( c-addr bytes buffer -- )
  DUP BUFFER-DATA-COUNT @ 2 PICK + OVER BUFFER-DATA-SIZE @ > IF
    2DUP EXPAND-BUFFER
  THEN
  DUP BUFFER-DATA @ OVER BUFFER-DATA @ 3 PICK +
  2 PICK BUFFER-DATA-COUNT @ MOVE 2 PICK OVER BUFFER-DATA @ 3 PICK MOVE
  DUP BUFFER-DATA-COUNT @ ROT + SWAP BUFFER-DATA-COUNT ! DROP ;

: PREPEND-BYTE-BUFFER ( c buffer -- )
  DUP BUFFER-DATA-COUNT @ OVER BUFFER-DATA-SIZE @ >= IF
    1 OVER EXPAND-BUFFER
  THEN
  DUP BUFFER-DATA @ OVER BUFFER-DATA @ 1+
  2 PICK BUFFER-DATA-COUNT @ MOVE TUCK BUFFER-DATA @ C!
  DUP BUFFER-DATA-COUNT @ 1+ SWAP BUFFER-DATA-COUNT ! ;

: WRITE-BUFFER ( c-addr bytes offset buffer -- )
  OVER 3 PICK + OVER BUFFER-DATA-SIZE @ > IF
    OVER 3 PICK + OVER BUFFER-DATA-COUNT @ - OVER EXPAND-BUFFER
  THEN
  3 ROLL OVER BUFFER-DATA @ 3 PICK + 4 PICK MOVE
  2 PICK 2 PICK + OVER BUFFER-DATA-COUNT @ > IF
    ROT ROT + SWAP BUFFER-DATA-COUNT !
  ELSE
    2DROP DROP
  THEN ;

: WRITE-BYTE-BUFFER ( c offset buffer -- )
  OVER 1+ OVER BUFFER-DATA-SIZE @ > IF
    OVER 1+ OVER BUFFER-DATA-COUNT @ - OVER EXPAND-BUFFER
  THEN
  ROT OVER BUFFER-DATA @ 3 PICK + C!
  OVER 1+ OVER BUFFER-DATA-COUNT @ > IF
    OVER 1+ SWAP BUFFER-DATA-COUNT !
  ELSE
    2DROP
  THEN ;

: INSERT-BUFFER ( c-addr bytes offset buffer -- )
  DUP BUFFER-DATA-COUNT @ 2 PICK >= IF
    WRITE-BUFFER
  ELSE
    DUP BUFFER-DATA-COUNT @ 3 PICK + OVER BUFFER-DATA-SIZE @ > IF
      2 PICK OVER EXPAND-BUFFER
    THEN
    DUP BUFFER-DATA @ 2 PICK + OVER BUFFER-DATA @ 3 PICK + 4 PICK +
    2 PICK BUFFER-DATA-COUNT @ 4 PICK - MOVE
    3 PICK OVER BUFFER-DATA @ 3 PICK + 4 PICK MOVE
    DUP BUFFER-DATA-COUNT @ 3 ROLL + SWAP BUFFER-DATA-COUNT ! 2DROP
  THEN ;

: INSERT-BYTE-BUFFER ( c offset buffer -- )
  DUP BUFFER-DATA-COUNT @ 2 PICK >= IF
    WRITE-BYTE-BUFFER
  ELSE
    DUP BUFFER-DATA-COUNT @ OVER BUFFER-DATA-SIZE @ >= IF
      1 OVER EXPAND-BUFFER
    THEN
    DUP BUFFER-DATA @ 2 PICK + OVER BUFFER-DATA @ 3 PICK + 1+
    2 PICK BUFFER-DATA-COUNT @ 4 PICK - MOVE
    ROT OVER BUFFER-DATA @ 3 PICK + C!
    DUP BUFFER-DATA-COUNT @ 1+ SWAP BUFFER-DATA-COUNT ! DROP
  THEN ;

: GET-BUFFER ( buffer -- c-addr bytes )
  DUP BUFFER-DATA @ SWAP BUFFER-DATA-COUNT @ ;

: CLEAR-BUFFER ( buffer -- ) 0 SWAP BUFFER-DATA-COUNT ! ;

SET-ORDER SET-CURRENT BASE !